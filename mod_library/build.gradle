apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

/*
 Project parameters are written in underscore_case to maintain readability from script codes.
 */
ext {
    // Bintray credentials
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    sonatype_userid = properties.getProperty("sonatype.userid")
    sonatype_passwd = properties.getProperty("sonatype.passwd")

    // bare minimum params for uploading archive
    package_name = 'AAR Jumpstart Kit'
    group_id = 'com.jattcode'
    artifact_id = 'aar-jumpstart-kit'
    lib_version = '0.6-SNAPSHOT'
    lib_version_code = 1
    git_url = 'https://github.com/jimcoven/android-bintray-kit.git'
    site_url = 'https://github.com/jimcoven/android-bintray-kit'
    all_licenses = ["Apache-2.0"]

    // pom.xml is required for users to download your aars using the command
    // ie. compile 'group_id:artifact_id:artifact_version'
    // eg. compile 'com.android.support:appcompat-v7:22.1.1'
    // params below are for defining the additional params for the pom.xml
    pom_library_desc = 'Basic android library template for deploying AARs'
    pom_license_name = 'The Apache Software License, Version 2.0'
    pom_license_url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'

    pom_developer_id = 'jimcoven'
    pom_developer_name = 'Jim Coven'
    pom_developer_email = ''

}

/* .............................................................
 * Modify the following if needed.
  ............................................................. */

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.1.1'
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 15
        versionCode lib_version_code
        versionName lib_version
        // android library does NOT need an app_id
    }

    buildTypes {
        release {
            minifyEnabled false // keep as false. No need proguard for OSS
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

apply from: '../aar-publish.gradle'
